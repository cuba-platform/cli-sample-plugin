buildscript {
    ext.kotlin_version = '1.2.50'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group 'com.haulmont.cuba.cli'
version '1.0.0'

def moduleName = "com.haulmont.cuba.cli.ideopener"

apply plugin: 'kotlin'
apply plugin: 'maven'

sourceCompatibility = 10
targetCompatibility = 10

repositories {
    jcenter()
    mavenLocal()
    
    maven { 
        url "https://dl.bintray.com/cuba-platform/main" 
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "com.haulmont.cuba.cli:cuba-cli:1.0.0"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

[compileKotlin, compileTestKotlin].each {
    it.kotlinOptions.jvmTarget = '1.8'
}

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--patch-module', "$moduleName=${compileKotlin.destinationDir}"
        ]
        classpath = files()
    }
}

task installPlugin(dependsOn: jar, type: Copy) {
    inputs.files jar.outputs.files

    jar.outputs.files.each {
        from it
    }

    into System.getProperty("user.home") + "/.haulmont/cli/plugins/"
}
