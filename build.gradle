buildscript {
    ext.kotlin_version = '1.2.41'

    repositories {
        mavenCentral()

        maven {
            credentials {
                username System.getenv('HAULMONT_REPOSITORY_USER') ?: 'cuba'
                password System.getenv('HAULMONT_REPOSITORY_PASSWORD') ?: 'cuba123'
            }
            url System.getenv('HAULMONT_REPOSITORY_URL') ?: 'https://repo.cuba-platform.com/content/groups/work'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group 'com.haulmont'
version '1.0-SNAPSHOT'

def moduleName = "cli.plugin.tutorial"

apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'application'

sourceCompatibility = 10
targetCompatibility = 10

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        credentials {
            username System.getenv('HAULMONT_REPOSITORY_USER') ?: 'cuba'
            password System.getenv('HAULMONT_REPOSITORY_PASSWORD') ?: 'cuba123'
        }
        url System.getenv('HAULMONT_REPOSITORY_URL') ?: 'https://repo.cuba-platform.com/content/groups/work'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "com.haulmont.cuba.cli:cuba-cli:1.0-SNAPSHOT"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

[compileKotlin, compileTestKotlin].each {
    it.kotlinOptions.jvmTarget = '1.8'
}

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--patch-module', "$moduleName=${compileKotlin.destinationDir}"
        ]
        classpath = files()
    }
}

task installPlugin(dependsOn: jar, type: Copy) {
    inputs.files jar.outputs.files

    jar.outputs.files.each {
        from it
    }

    into System.getProperty("user.home") + "/.haulmont/cli/plugins/"
}